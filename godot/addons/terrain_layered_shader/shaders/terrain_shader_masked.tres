[gd_resource type="Shader" format=3 uid="uid://cj5ui8mbwgx27"]

[resource]
code = "/**
* MIT License
*
* Copyright (c) 2023 Mark McKay
* https://github.com/blackears/terrain_layered_shader
*
* Permission is hereby granted, free of charge, to any person obtaining a copy
* of this software and associated documentation files (the \"Software\"), to deal
* in the Software without restriction, including without limitation the rights
* to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
* copies of the Software, and to permit persons to whom the Software is
* furnished to do so, subject to the following conditions:
*
* The above copyright notice and this permission notice shall be included in all
* copies or substantial portions of the Software.
*
* THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
* AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
* OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
* SOFTWARE.
*/


shader_type spatial;
render_mode blend_mix, depth_draw_never, cull_back, diffuse_burley, specular_schlick_ggx;

#include \"res://addons/terrain_layered_shader/shaders/terrain_tools.gdshaderinc\"

uniform float tile_margin : hint_range(0, 1) = .1;
uniform vec2 uv_terrain_scale = vec2(1.0, 1.0);
uniform vec2 uv_terrain_offset;

group_uniforms Albedo;
uniform vec4 albedo : source_color = vec4(1.0, 1.0, 1.0, 1.0);
uniform sampler2D texture_albedo : source_color, filter_linear_mipmap, repeat_enable;

group_uniforms Metalic;
uniform float metallic;
uniform sampler2D texture_metallic : filter_linear_mipmap, repeat_enable, hint_default_white;
uniform vec4 metallic_texture_channel;

group_uniforms Roughness;
uniform float roughness : hint_range(0, 1) = 1.0;
uniform sampler2D texture_roughness : hint_roughness_r, filter_linear_mipmap, repeat_enable;

group_uniforms Specular;
uniform float specular;

group_uniforms Normal;
uniform sampler2D texture_normal : hint_roughness_normal, filter_linear_mipmap, repeat_enable;
uniform float normal_scale : hint_range(-16, 16) = 1.0;

group_uniforms Height;
uniform sampler2D texture_heightmap : hint_default_black,filter_linear_mipmap,repeat_enable;
uniform bool deep_parallax = false;
uniform float heightmap_scale = 5.0;
uniform int heightmap_min_layers = 8;
uniform int heightmap_max_layers = 32;
uniform vec2 heightmap_flip = vec2(1, 1);

group_uniforms Mask;
uniform vec4 mask_channel : source_color = vec4(0.0, 0.0, 0.0, 0.0);
uniform sampler2D texture_mask : source_color, filter_linear_mipmap, repeat_enable, hint_default_black;
uniform float mask_scale = 1.0;
uniform vec2 uv_mask_scale = vec2(1.0, 1.0);
uniform vec2 uv_mask_offset;

varying vec2 mask_uv;
varying vec2 terrain_uv;

void vertex() {
	mask_uv = UV * uv_mask_scale.xy + uv_mask_offset.xy;
	terrain_uv = UV * uv_terrain_scale.xy + uv_terrain_offset.xy;
}


void fragment() {
	vec2 base_uv = terrain_uv;
	if (!deep_parallax)
	{
		vec3 view_dir = normalize(normalize(-VERTEX + EYE_OFFSET) * mat3(TANGENT * heightmap_flip.x, -BINORMAL * heightmap_flip.y, NORMAL));
		float depth = 1.0 - texture(texture_heightmap, base_uv).r;
		vec2 ofs = base_uv - view_dir.xy * depth * heightmap_scale * 0.01;
		base_uv=ofs;
	}
	else
	{
		vec3 view_dir = normalize(normalize(-VERTEX + EYE_OFFSET) * mat3(TANGENT * heightmap_flip.x, -BINORMAL * heightmap_flip.y, NORMAL));
		float num_layers = mix(float(heightmap_max_layers),float(heightmap_min_layers), abs(dot(vec3(0.0, 0.0, 1.0), view_dir)));
		float layer_depth = 1.0 / num_layers;
		float current_layer_depth = 0.0;
		vec2 P = view_dir.xy * heightmap_scale * 0.01;
		vec2 delta = P / num_layers;
		vec2 ofs = base_uv;
		float depth = 1.0 - texture(texture_heightmap, ofs).r;
		float current_depth = 0.0;
		while(current_depth < depth) {
			ofs -= delta;
			depth = 1.0 - texture(texture_heightmap, ofs).r;
			current_depth += layer_depth;
		}
		vec2 prev_ofs = ofs + delta;
		float after_depth  = depth - current_depth;
		float before_depth = ( 1.0 - texture(texture_heightmap, prev_ofs).r  ) - current_depth + layer_depth;
		float weight = after_depth / (after_depth - before_depth);
		ofs = mix(ofs,prev_ofs,weight);
		base_uv=ofs;
	}
	
	vec4 albedo_tex = sample_tiling(texture_albedo, base_uv, tile_margin);
	ALBEDO = albedo.rgb * albedo_tex.rgb;
	float metallic_tex = dot(sample_tiling(texture_metallic, base_uv, tile_margin), metallic_texture_channel);
	METALLIC = metallic_tex * metallic;
	vec4 roughness_texture_channel = vec4(1.0, 0.0, 0.0, 0.0);
	float roughness_tex = dot(sample_tiling(texture_roughness, base_uv, tile_margin), roughness_texture_channel);
	ROUGHNESS = roughness_tex * roughness;
	NORMAL_MAP = sample_tiling(texture_normal, base_uv, tile_margin).rgb;
	NORMAL_MAP_DEPTH = normal_scale;
	
	SPECULAR = specular;
	
	ALPHA = dot(texture(texture_mask, mask_uv) * mask_scale, mask_channel);
}
"
